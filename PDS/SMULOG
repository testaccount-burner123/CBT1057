/* REXX    SMULOG     Log the Roll action from SHOWMEM.

           Written by G052811 Chris Lewis 19970502

     Impact Analysis
.    SYSEXEC   RUNDATA
.    SYSEXEC   TRAPOUT

     Modification History
     20211124 fxc REXXSKEL v.970211 to v.20210402
     20240423 fxc DUMP_QUEUE quiet;
     20240513 fxc changed SYSPROC to SYSEXEC in I/A;
     20241104 fxc add RUNDATA to I/A;

*/ arg argline
address TSO                            /* REXXSKEL ver.20210402      */
arg parms "((" opts

signal on syntax
signal on novalue

call TOOLKIT_INIT                      /* conventional start-up     -*/
rc     = trace(tv)
info   = parms                         /* to enable parsing          */

call A_IGNITION                        /*                           -*/
call B_LOG                             /*                           -*/

if \sw.nested then call DUMP_QUEUE 'quiet' /*                       -*/
exit                                   /*@ SMULOG                    */
/*
.  ----------------------------------------------------------------- */
A_IGNITION:                            /*@                           */
   if branch then call BRANCH
   address TSO

   vb255.0 = "NEW CATALOG UNIT( SYSDA ) TRACKS SPACE( 2 5 )",
             "RECFM( V B ) LRECL( 255 ) BLKSIZE( 0 ) DSORG( PS )"
   vb255.1 = "MOD"

   "NEWSTACK"
   "RUNDATA READ  TBLKEY SHOWMEM "     /* sets LOGDSN                */
   do queued()                         /* return from RUNDATA        */
      pull tag tagval
      if tag = "<ERROR>" then do
         sw.0Error_Found = 1
         "DELSTACK"
         say tag tagval
         return
         end
      tagval  = Space( tagval,1 )
      @z   = Value( tag,tagval )       /* tag <-- tagval             */
   end                                 /* queued                     */
   if Symbol( 'logdsn' ) = 'LIT' then do
      helpmsg = "RUNDATA did not supply a required value. ",
                "Correct the RUNDATA table as specified",
                "in the installation guide",
                "before proceeding. "
      say helpmsg
      exit
      end
   "DELSTACK"
   xef     = 'ef'X

   call AA_KEYWDS                      /*                           -*/

return                                 /*@ A_IGNITION                */
/*
.  ----------------------------------------------------------------- */
AA_KEYWDS:                             /*@                           */
   if branch then call BRANCH
   address TSO

   element     = KEYWD("ELEMENT")      /* CMDSTVON                   */
   type        = KEYWD("TYPE")         /* CLIST                      */
   action      = KEYWD("ACTION")       /* ADD                        */
   version     = KEYWD("VERSION")      /* 01                         */
   level       = KEYWD("LEVEL")        /* 22                         */
   create      = KEYWD("CREATE")       /* 97/01/01                   */
   change      = KEYWD("CHANGE")       /* 97/06/06                   */
   size        = KEYWD("SIZE")         /* 1433                       */
   init        = KEYWD("INIT")         /* 9                          */
   mod         = KEYWD("MOD")          /* 0                          */
   id          = KEYWD("ID")           /* G052811                    */

return                                 /*@ AA_KEYWDS                 */
/*
.  ----------------------------------------------------------------- */
B_LOG:                                 /*@                           */
   if branch then call BRANCH
   address TSO

   do 5
      status = sysdsn(logdsn) = "OK"
     "ALLOC F(LOGF) DA("logdsn") REUSE" vb255.status
      rcx    = rc
      if rcx = 0 then leave
   end

   if rcx > 0 then return

  "NEWSTACK"
   push left(action,4) element type userid() date("S") time(),
        xef version"."level create change size init mod id

  "EXECIO" queued() "DISKW LOGF (FINIS"
   rcx = rc
  "FREE F(LOGF)"
  "DELSTACK"

return                                 /*@ B_LOG                     */
/*
.  ----------------------------------------------------------------- */
LOCAL_PREINIT:                         /*@ customize opts            */
   address TSO


return                                 /*@ LOCAL_PREINIT             */
/*   subroutines below LOCAL_PREINIT are not selected by SHOWFLOW    */
/*
.  ----------------------------------------------------------------- */
HELP:                                  /*@                           */
address TSO;"CLEAR"
if helpmsg <> "" then do ; say helpmsg; say ""; end
ex_nam = Left(exec_name,8)             /* predictable size           */
say "  "ex_nam"      logs activity to the user's log table.  This routine is   "
say "                not intended to be called manually.  It is designed to be "
say "                called by another process that has been programmed to     "
say "                insert the proper parameters.                             "
say "                                                                          "
say "  Syntax:   "ex_nam"  ELEMENT <elemnm>                          (Required)"
say "                      TYPE    <type>                            (Required)"
say "                      ACTION  <action>                          (Required)"
say "                      VERSION <version>                         (Required)"
say "                      LEVEL   <level>                           (Required)"
say "                      CREATE  <cre-dt>                          (Required)"
say "                      CHANGE  <chg-dt>                          (Required)"
say "                      SIZE    <size>                            (Required)"
say "                      INIT    <initsz>                          (Required)"
say "                      MOD     <mod>                             (Required)"
say "                      ID      <userid>                          (Required)"
"NEWSTACK"; pull ; "CLEAR" ; "DELSTACK"
say "            elemnm    The name of the software element.                   "
say "                                                                          "
say "            type      the data type                                       "
say "                                                                          "
say "            action    the action performed                                "
say "                                                                          "
say "            version   the statistics version                              "
say "                                                                          "
say "            level     the statistics level                                "
say "                                                                          "
say "            cre-dt    the create date as yy/mm/dd                         "
say "                                                                          "
say "            chg-dt    the create date as yy/mm/dd                         "
say "                                                                          "
say "            size      current line count                                  "
say "                                                                          "
say "            initsz    original line count                                 "
say "                                                                          "
say "            mod       modified lines                                      "
say "                                                                          "
say "            userid    the TSO Userid                                      "
"NEWSTACK"; pull ; "CLEAR" ; "DELSTACK"
say "   Debugging tools provided include:                                      "
say "                                                                          "
say "        MONITOR:  displays key information throughout processing.         "
say "                                                                          "
say "        NOUPDT:   by-pass all update logic.                               "
say "                                                                          "
say "        BRANCH:   show all paragraph entries.                             "
say "                                                                          "
say "        TRACE tv: will use value following TRACE to place the             "
say "                  execution in REXX TRACE Mode.                           "
say "                                                                          "
say "                                                                          "
say "   Debugging tools can be accessed in the following manner:               "
say "                                                                          "
say "        TSO "ex_nam"  parameters     ((  debug-options                    "
say "                                                                          "
say "   For example:                                                           "
say "                                                                          "
say "        TSO "ex_nam"  (( MONITOR TRACE ?R                                 "

address ISPEXEC "CONTROL DISPLAY REFRESH"
exit                                   /*@ HELP                      */
/*
.  ----------------------------------------------------------------- */
BRANCH: Procedure expose,              /*@                           */
        sigl exec_name
   rc = trace("O")                     /* we do not want to see this */
   arg brparm .

   origin = sigl                       /* where was I called from ?  */
   do currln = origin to 1 by -1       /* inch backward to label     */
      if Right(Word(Sourceline(currln),1),1) = ":" then do
         parse value sourceline(currln) with pgfname ":" .  /* Label */
         leave ; end                   /*                name        */
   end                                 /* currln                     */

   select
      when brparm = "NAME" then return(pgfname) /* Return full name  */
      when brparm = "ID"      then do           /* wants the prefix  */
         parse var pgfname pgfpref "_" .        /* get the prefix    */
         return(pgfpref)
         end                           /* brparm = "ID"              */
      otherwise
         say left(sigl,6) left(pgfname,40) exec_name "Time:" time("L")
   end                                 /* select                     */

return                                 /*@ BRANCH                    */
/*
.  ----------------------------------------------------------------- */
DUMP_QUEUE:                            /*@ Take whatever is in stack */
   rc = trace("O")                     /*  and write to the screen   */
   address TSO
   arg mode .

   "QSTACK"                            /* how many stacks?           */
   stk2dump    = rc - tk_init_stacks   /* remaining stacks           */
   if stk2dump = 0 & queued() = 0 then return
   if mode <> "QUIET" then,
   say "Total Stacks" rc ,             /* rc = #of stacks            */
    "   Begin Stacks" tk_init_stacks , /* Stacks present at start    */
    "   Excess Stacks to dump" stk2dump

   do dd = rc to tk_init_stacks by -1  /* empty each one.            */
      if mode <> "QUIET" then,
      say "Processing Stack #" dd "   Total Lines:" queued()
      do queued();parse pull line;say line;end /* pump to the screen */
      "DELSTACK"                       /* remove stack               */
   end                                 /* dd = 1 to rc               */

return                                 /*@ DUMP_QUEUE                */
/* Handle CLIST-form keywords             added 20020513
.  ----------------------------------------------------------------- */
CLKWD: Procedure expose info           /*@ hide all except info      */
   arg kw
   kw = kw"("                          /* form is 'KEY(DATA)'        */
   kw_pos = Pos(kw,info)               /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   rtpt   = Pos(") ",info" ",kw_pos)   /* locate end-paren           */
   slug   = Substr(info,kw_pos,rtpt-kw_pos+1)     /* isolate         */
   info   = Delstr(info,kw_pos,rtpt-kw_pos+1)     /* excise          */
   parse var slug (kw)     slug        /* drop kw                    */
   slug   = Reverse(Substr(Reverse(Strip(slug)),2))
return slug                            /*@CLKWD                      */
/* Handle multi-word keys 20020513
.  ----------------------------------------------------------------- */
KEYWD: Procedure expose info           /*@ hide all vars, except info*/
   arg kw
   kw_pos = wordpos(kw,info)           /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   kw_val = word(info,kw_pos+Words(kw))/* get the next word          */
   info   = Delword(info,kw_pos,2)     /* remove both                */
return kw_val                          /*@ KEYWD                     */
/*
.  ----------------------------------------------------------------- */
KEYPHRS: Procedure expose,             /*@                           */
         info helpmsg exec_name        /*  except these three        */
   arg kp
   wp    = wordpos(kp,info)            /* where is it?               */
   if wp = 0 then return ""            /* not found                  */
   front = subword(info,1,wp-1)        /* everything before kp       */
   back  = subword(info,wp+1)          /* everything after kp        */
   parse var back dlm back             /* 1st token must be 2 bytes  */
   if length(dlm) <> 2 then            /* Must be two bytes          */
      helpmsg = helpmsg,
         "Invalid length for delimiter("dlm") with KEYPHRS("kp")",
         info
   if wordpos(dlm,back) = 0 then       /* search for ending delimiter*/
      helpmsg = helpmsg,
         "No matching second delimiter("dlm") with KEYPHRS("kp")",
         info
   if helpmsg <> "" then call HELP     /* Something is wrong         */
   parse var back kpval (dlm) back     /* get everything b/w delim   */
   info =  front back                  /* restore remainder          */
return Strip(kpval)                    /*@ KEYPHRS                   */
/*
.  ----------------------------------------------------------------- */
NOVALUE:                               /*@                           */
   say exec_name "raised NOVALUE at line" sigl
   say " "
   say "The referenced variable is" condition("D")
   say " "
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ NOVALUE                   */
/*
.  ----------------------------------------------------------------- */
SHOW_SOURCE:                           /*@                           */
   call DUMP_QUEUE                     /* Spill contents of stacks  -*/
   if sourceline() <> "0" then         /* to screen                  */
      say sourceline(zsigl)
   rc =  trace("?R")
   nop
   exit                                /*@ SHOW_SOURCE               */
/*
.  ----------------------------------------------------------------- */
SS: Procedure                          /*@ Show Source               */
   arg  ssbeg  ssend  .
   if ssend = "" then ssend = 10
   if \datatype(ssbeg,"W") | \datatype(ssend,"W") then return
   ssend = ssbeg + ssend
   do ssii = ssbeg to ssend ; say Strip(sourceline(ssii),'T') ; end
return                                 /*@ SS                        */
/*
.  ----------------------------------------------------------------- */
SWITCH: Procedure expose info          /*@                           */
   arg kw
   sw_val = Wordpos(kw,info) > 0       /* exists = 1; not found = 0  */
   if sw_val then                      /* exists                     */
      info = Delword(info,Wordpos(kw,info),1) /* remove it           */
return sw_val                          /*@ SWITCH                    */
/*
.  ----------------------------------------------------------------- */
SYNTAX:                                /*@                           */
   errormsg = exec_name "encountered REXX error" rc "in line" sigl":",
                        errortext(rc)
   say errormsg
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ SYNTAX                    */
/*
   Can call TRAPOUT.
.  ----------------------------------------------------------------- */
TOOLKIT_INIT:                          /*@                           */
   address TSO
   info = Strip(opts,"T",")")          /* clip trailing paren        */

   parse source  sys_id  how_invokt  exec_name  DD_nm  DS_nm,
                     as_invokt  cmd_env  addr_spc  usr_tokn

   parse value "" with  tv  helpmsg  .
   parse value 0   "ISR00000  YES"     "Error-Press PF1"    with,
               sw.  zerrhm    zerralrm  zerrsm

   if SWITCH("TRAPOUT") then do
      "TRAPOUT" exec_name parms "(( TRACE R" info
      exit
      end                              /* trapout                    */

   sw.nested    = sysvar("SYSNEST") = "YES"
   sw.batch     = sysvar("SYSENV")  = "BACK"
   sw.inispf    = sysvar("SYSISPF") = "ACTIVE"

   if Word(parms,1) = "?" then call HELP /* I won't be back          */

   "QSTACK" ; tk_init_stacks = rc      /* How many stacks?           */

   parse value SWITCH("BRANCH") SWITCH("MONITOR") SWITCH("NOUPDT") with,
               branch           monitor           noupdt    .

   parse value mvsvar("SYSNAME") sysvar("SYSNODE") with,
               #tk_cpu           node          .

   parse value KEYWD("TRACE")  "O"    with   tv  .
   tk_globalvars = "exec_name  tv  helpmsg  sw.  zerrhm  zerralrm ",
                   "zerrsm  zerrlm  tk_init_stacks  branch  monitor ",
                   "noupdt"

   call LOCAL_PREINIT                  /* for more opts             -*/

return                                 /*@ TOOLKIT_INIT              */
